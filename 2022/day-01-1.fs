S" day-01-input.txt" OPEN-BLOCKS

VARIABLE BLOCK-NUMBER   ( CURRENT, NOT NEXT, BLOCK NUMBER. )
VARIABLE BLOCK-START
VARIABLE BLOCK-POS

0 BLOCK-NUMBER !
0 BLOCK DUP BLOCK-START ! BLOCK-POS !

( IF AT THE END OF THE BLOCK, FETCH THE NEXT ONE. )
: ENSURE-BLOCK ( -- )
    BLOCK-POS @ BLOCK-START @ - 1024 =  ( END? )
    IF
	BLOCK-NUMBER @ 1+ DUP           ( NUM+1 NUM+1 )
	BLOCK DUP                       ( NUM+1 START START )
	BLOCK-START ! BLOCK-POS ! BLOCK-NUMBER !
    THEN
;

: PEEK-CHAR ( -- CHAR ) ENSURE-BLOCK BLOCK-POS @ C@ ;
: GET-CHAR ( -- CHAR )
    ENSURE-BLOCK
    BLOCK-POS DUP @ DUP C@ ( @POS POS CHAR )
    SWAP 1+ ROT !
;
: ?NOT-EOF ( -- FLAG ) PEEK-CHAR 26 <> ;

( FETCH THE NEXT LINE OF INPUT, INCLUDING NEWLINE, INTO PAD.  )
( COPY CHARACTER-BY-CHARACTER TO HANDLE THE END OF THE BLOCK. )
( ASSUME NOT AT EOF.  RETURNS FALSE FOR BLANK LINES. )
: GET-NEXT-LINE ( -- FLAG )
    PAD                           ( DST )
    BEGIN
	GET-CHAR DUP 10 <>        ( DST CHAR NOT-CR? )
    WHILE
	    OVER C! 1+            ( DST+1 )
    REPEAT
    OVER C! PAD <>  ( WRITE THE CR AS WELL )
;

VARIABLE MAXIMUM
VARIABLE ACCUMULATOR

0 MAXIMUM !
0 ACCUMULATOR !

: GET-MAX ( N1 N2 -- N ) OVER OVER > IF DROP ELSE SWAP DROP THEN ;

: RUN ( -- )
    BEGIN
	?NOT-EOF
    WHILE
	    GET-NEXT-LINE
	    IF
		0 0 PAD 1- CONVERT DROP DROP  ( N )
		ACCUMULATOR DUP @ ROT         ( @ACC ACC N )
		+ SWAP !
	    ELSE
		MAXIMUM DUP @ ACCUMULATOR @   ( @MAX MAX ACC )
		GET-MAX SWAP !
		0 ACCUMULATOR !
	    THEN
    REPEAT
;

RUN
MAXIMUM @ . CR
BYE
